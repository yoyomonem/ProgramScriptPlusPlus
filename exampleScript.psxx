def isNumber(var string)
    const var stringSize = len(string)
    const var numberChrs = ["0" "1" "2" "3" "4" "5" "6" "7" "8" "9"]
    var chrFound = false
    var chrIndex = 0
    var currentChr
    var numberChrIndex
    var foundNumberChr

    while chrIndex < stringSize && !chrFound
        currentChr = at(string chrIndex)
        numberChrIndex = 0
        foundNumberChr = false

        while numberChrIndex < 10 && !foundNumberChr
            if currentChr == at(numberChrs numberChrIndex)
                foundNumberChr = true
            end

            numberChrIndex++
        end

        if !foundNumberChr
            chrFound = true
        end

        chrIndex++
    end

    !chrFound
end

def sort(var array)
    const var arraySize = len(array)
    var arrayIndex = 0
    var currentElem
    var nextElem
    var sorted = false

    while !sorted
        arrayIndex = 0
        sorted = true

        while arrayIndex < arraySize - 1
            currentElem = at(array arrayIndex)
            nextElem = at(array arrayIndex+1)

            if currentElem > nextElem
                set(array arrayIndex nextElem)
                set(array arrayIndex+1 currentElem)
                sorted = false
            else 
                arrayIndex++
            end
        end
    end

    array
end

def main()
    noodle input = ""
    noodle numbers = []

    print("Welecome to the number sorter! Type `sort` to sort numbers.\n\n")

    while input != "sort"
        serve("Type a number to add to the list: ")
        input = receive()

        if isNumber(input)
            append(numbers num(input))
        else
            if input != "sort"
                serve(" " input " is not a valid number\n")
            end
        end
    end

    print("\n Original number list: " numbers "\n")
    print(" Sorted number list: " sort(numbers) "\n\n")
end

main()
